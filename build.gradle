// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    ext {
        applicationId = 'com.vginert.cleanarchitectureplayground'
        versionCode = 1
        versionName = "1.0"
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = applicationId + '.test'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task runDomainUnitTests(dependsOn: [':domain:test']) {
    description 'Run unit tests for the domain layer.'
}

task runDataUnitTests(dependsOn: [':data:cleanTestDebugUnitTest', ':data:testDebugUnitTest']) {
    description 'Run unit tests for the data layer.'
}

task runUnitTests(dependsOn: ['runDomainUnitTests', 'runDataUnitTests']) {
    description 'Run unit tests for both domain and data layers.'
}

task runAcceptanceTests(dependsOn: [':android:connectedAndroidTest']) {
    description 'Run application acceptance tests.'
}

ext {
    //Android
    buildToolsVersion = "26.0.2"
    minSdkVersion = 19
    targetSdkVersion = 26
    compileSdkVersion = 26

    //Libraries
    androidSupportVersion = '26.0.2'
    leakCanaryVersion = '1.5.4'
    javaxAnnotationVersion = '1.0'
    javaxInjectVersion = '1'
    daggerVersion = '2.12'
    rxJavaVersion = '2.1.6'
    rxAndroidVersion = '2.0.1'
    retrofitVersion = '2.3.0'

    //Testing
    jUnitVersion = '4.12'
    assertJVersion = '1.7.1'
    mockitoVersion = '1.9.5'

    androidDependencies = [
            androidSupport   : "com.android.support:appcompat-v7:${androidSupportVersion}",
            leakCanaryDebug  : "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}",
            leakCanaryRelease: "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}",
            javaxAnnotation  : "javax.annotation:jsr250-api:${javaxAnnotationVersion}",
            daggerCompiler   : "com.google.dagger:dagger-compiler:${daggerVersion}",
            dagger           : "com.google.dagger:dagger:${daggerVersion}",
            rxJava           : "io.reactivex.rxjava2:rxjava:${rxJavaVersion}",
            rxAndroid        : "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}",
    ]

    domainDependencies = [
            javaxAnnotation: "javax.annotation:jsr250-api:${javaxAnnotationVersion}",
            javaxInject    : "javax.inject:javax.inject:${javaxInjectVersion}",
            rxJava         : "io.reactivex.rxjava2:rxjava:${rxJavaVersion}",
    ]

    domainTestDependencies = [
            junit:              "junit:junit:${jUnitVersion}",
            mockito:            "org.mockito:mockito-core:${mockitoVersion}",
            assertj:            "org.assertj:assertj-core:${assertJVersion}"
    ]

    dataDependencies = [
            javaxAnnotation       : "javax.annotation:jsr250-api:${javaxAnnotationVersion}",
            javaxInject           : "javax.inject:javax.inject:${javaxInjectVersion}",
            androidAnnotations    : "com.android.support:support-annotations:${androidSupportVersion}",
            retrofit              : "com.squareup.retrofit2:retrofit:${retrofitVersion}",
            retrofitGsonConverter : "com.squareup.retrofit2:converter-gson:${retrofitVersion}",
            retrofitRxjava2Apadter: "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    ]
}